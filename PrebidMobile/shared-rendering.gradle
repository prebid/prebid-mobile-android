apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.minSDKVersion
        targetSdkVersion rootProject.targetSDKVersion
        versionCode rootProject.prebidVersionCode
        versionName rootProject.prebidVersionName
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/LICENSE'
    }

    testOptions {
        unitTests {
            includeAndroidResources true
            returnDefaultValues = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    testImplementation project(':TestUtils')
}

task javadoc(type: Javadoc) {
    failOnError false

    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}