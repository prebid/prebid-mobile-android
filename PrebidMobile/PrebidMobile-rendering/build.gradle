/*
 *    Copyright 2018-2021 Prebid.org, Inc.
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */
apply from: '../shared-rendering.gradle'
apply from: 'publish-rendering-sdk.gradle'
apply from: 'publish-omsdk.gradle'

android {
    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    defaultConfig {
        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
        buildConfigField "String", "VERSION", "\"${versionName}\""
        buildConfigField "String", "OMSDK_VERSION", "\"${omSdkVersion}\""
    }

    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "2g"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            systemProperty 'robolectric.dependency.repo.url', 'https://repo1.maven.org/maven2'
            testLogging {
                exceptionFormat = "full"
            }
        }
    }

    defaultConfig {
        consumerProguardFiles file('proguard-rules.pro')
    }
}

dependencies {
    // AndroidJUnitRunner and JUnit Rules
    testImplementation project(':TestUtils')

    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'androidx.test:rules:1.2.0'

    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.12.4'

    testImplementation 'org.json:json:20180813'
    testImplementation 'com.google.code.gson:gson:2.8.6'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.0'

    // Google play services
    implementation project(":PrebidMobile-core")

    implementation 'com.google.android.gms:play-services-base:17.6.0'
    implementation 'com.google.android.gms:play-services-ads-identifier:17.0.0'
    implementation 'com.google.android.exoplayer:exoplayer-core:2.13.3'
    implementation 'com.google.android.exoplayer:exoplayer-ui:2.13.3'

    implementation 'androidx.annotation:annotation:1.1.0'
    implementation project(path: ':omsdk-android')
}