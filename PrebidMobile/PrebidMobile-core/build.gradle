apply from: '../android.gradle'
apply from: '../publisher.gradle'
apply from: '../tests.gradle'

android {
    namespace "org.prebid.mobile.core"
    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    defaultConfig {
        buildConfigField "String", "GitHash", "\"${getGitHash()}\""
        buildConfigField "String", "VERSION", "\"${versionName}\""
        buildConfigField "String", "OMSDK_VERSION", "\"${omSdkVersion}\""
        multiDexEnabled true
    }
    testOptions {
        unitTests.all {
            jvmArgs '--add-opens=java.base/java.lang=ALL-UNNAMED'
            systemProperty 'robolectric.invokedynamic.enable', 'false'
        }
    }
}


dependencies {
    implementation project(":omsdk-android")

    implementation libs.androidx.annotation

    implementation libs.google.play.services.base
    implementation libs.google.play.services.ads.identifier
    implementation libs.exoplayer.core
    implementation libs.exoplayer.ui

    implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.0.0"
}